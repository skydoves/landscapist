plugins {
  id 'com.android.library'
  id 'kotlin-android'
  id 'org.jetbrains.dokka'
}

apply from: "$rootDir/dependencies.gradle"

android {
  compileSdkVersion versions.compileSdk

  defaultConfig {
    minSdkVersion versions.minSdk
    targetSdkVersion versions.compileSdk
    versionCode versions.versionCode
    versionName versions.versionName
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  composeOptions {
    kotlinCompilerExtensionVersion versions.composeVersion
  }

  kotlinOptions {
    jvmTarget = '1.8'
    freeCompilerArgs += "-Xallow-jvm-ir-dependencies"
    freeCompilerArgs += "-Xskip-prerelease-check"
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    useIR = true
  }

  buildFeatures {
    buildConfig false
    compose true
  }

  packagingOptions {
    excludes += "/META-INF/AL2.0"
    excludes += "/META-INF/LGPL2.1"
  }

  testOptions {
    unitTests {
      includeAndroidResources = true
    }
  }
}

kotlin {
  explicitApi()
}

dependencies {
  api project(":landscapist")
  api "io.coil-kt:coil:$versions.coilVersion"

  implementation "androidx.core:core-ktx:$versions.coreKtxVersion"
  implementation "androidx.compose.ui:ui:$versions.composeVersion"
  implementation "androidx.compose.runtime:runtime:$versions.composeVersion"
  implementation "androidx.compose.foundation:foundation:$versions.composeVersion"

  androidTestImplementation "androidx.compose.ui:ui:$versions.composeVersion"
  androidTestImplementation "androidx.compose.ui:ui-test-junit4:$versions.composeVersion"
  androidTestImplementation "androidx.test:rules:$versions.rulesVersion"
}

afterEvaluate {
  tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
    outputDirectory.set(rootProject.file('docs/api'))
  }
}

dokkaHtml.configure {
  dokkaSourceSets {
    named("main") {
      noAndroidSdkLink.set(false)
    }
  }
}

tasks.withType(Javadoc).all { enabled = false }

ext.repository = [
    name       : "landscapist-coil",
    description: "Jetpack Compose image library for requesting and loading images using Coil.",
    websiteUrl : "https://github.com/skydoves/Landscapist",
    scm        : 'scm:git@github.com/skydoves/Landscapist.git'
]

apply from: "$rootDir/spotless.gradle"
apply from: "$rootDir/publish.gradle"